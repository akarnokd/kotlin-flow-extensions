version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.3.71'

    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "biz.aQute.bnd:biz.aQute.bnd.gradle:5.0.1"
        classpath 'com.vanniktech:gradle-maven-publish-plugin:0.8.0'
        classpath "gradle.plugin.com.hierynomus.gradle.plugins:license-gradle-plugin:0.15.0"
    }
}

apply plugin: 'kotlin'
apply plugin: 'jacoco'

group = "com.github.akarnokd"
ext.githubProjectName = 'kotin-flow-extensions'

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.5'
    testCompile 'junit:junit:4.13'
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
}

jacoco {
    toolVersion = "0.8.5"
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

apply plugin: 'maven'

apply plugin: 'biz.aQute.bnd.builder'

jar {
    bnd ('Bundle-Name': 'kotlin-flow-extensions',
            'Bundle-Vendor': 'akarnokd',
            'Bundle-Description': 'Extensions to the Kotlin Flow library.',
            'Import-Package': '!org.junit,!junit.framework,!org.mockito.*,*',
            'Bundle-DocURL': 'https://github.com/akarnokd/kotlin-flow-extensions')
}

apply plugin: "com.vanniktech.maven.publish"

test {
    maxHeapSize = "1g"
    testLogging  {
        events "started", "failed" // "skipped", "passed"
        exceptionFormat="full"
        //    showStandardStreams = true
    }
}

build.dependsOn jacocoTestReport

check.dependsOn jacocoTestReport

apply plugin: "com.github.hierynomus.license"

license {
    header rootProject.file('HEADER')
    mapping('kt', 'SLASHSTAR_STYLE')
    skipExistingHeaders true
    ignoreFailures true
    excludes(["**/*.md", "**/*.txt"])
}